plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

dependencies {
    api('jakarta.annotation:jakarta.annotation-api') { version { strictly '[3.0.0,)'; prefer '3.0.0' } }
    api('com.google.code.gson:gson') { version { strictly '[2.11.0,)'; prefer '2.11.0' } }
    api('com.google.guava:guava') { version { strictly '[33.3.0-jre,)'; prefer '33.3.0-jre' } }
    api('commons-io:commons-io') { version { strictly '[2.16.1,)'; prefer '2.16.1' } }
    api('org.moddingx:CurseWrapper') { version { strictly '[5.0,6.0)'; prefer '5.0' } }
    api('org.moddingx:LauncherLib') { version { strictly '[2.1.0,3.0.0)'; prefer '2.1.0' } }
}

gradlePlugin {
    plugins {
        packdev {
            id = 'org.moddingx.packdev'
            implementationClass = 'org.moddingx.packdev.PackDevPlugin'
        }
    }
}

jar {
    dependsOn project(':plugin-forge').jar
    dependsOn project(':plugin-fabric').jar
    dependsOn project(':plugin-neoforge').jar
    
    from zipTree(project(':plugin-forge').jar.archiveFile), { exclude 'META-INF/**' }
    from zipTree(project(':plugin-fabric').jar.archiveFile), { exclude 'META-INF/**' }
    from zipTree(project(':plugin-neoforge').jar.archiveFile), { exclude 'META-INF/**' }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = rootProject.group
            artifactId = rootProject.name
            version = rootProject.version
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'moddingx'
            url 'https://maven.moddingx.org/release'
            credentials(PasswordCredentials)
        }
    }
}
